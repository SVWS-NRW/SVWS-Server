configurations {
	implementation.extendsFrom(crypto, database, jackson, validation)
	dto
}

dependencies {
    implementation project(':svws-base')
    implementation project(':svws-core')

    dto fileTree(dir: "${project.rootProject.projectDir}/svws-base/src/main/resources/schema", includes: ['**/*.csv'])
}

publishing {
    publications {
        mavenSVWSDB(MavenPublication) {
            artifactId = 'svws-db'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'SVWS DB Library'
                description = 'Diese Bibliothek regelt den Zugriff auf Datenbanken f√ºr die Schulverwaltungssoftware in NRW'
                url = 'http://www.svws.nrw.de'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
        			connection = 'scm:git@github.com/SVWS-NRW/SVWS-Packages'
        			developerConnection = 'scm:git@github.com/SVWS-NRW/SVWS-Packages.git'
                    url = 'https://github.com/SVWS-NRW/SVWS-Packages.git'
                }
            }
        }
    }
}

task createJavaDTOs(type: JavaExec) {
	group = 'svws'
	dependsOn compileJava
    doFirst {
		delete "$rootDir/svws-db-dto/src/main/java/de/nrw/schule/svws/db/dto"
	}
	inputs.files sourceSets.main.java, configurations.dto.files
    outputs.files fileTree(dir: "$rootDir/svws-db-dto/src/main/java/de/nrw/schule/svws/db/dto"), "$rootDir/svws-db/src/main/resources/META-INF/persistence.xml"
    description "Erzeuge Java-DTOs"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'de.nrw.schule.svws.db.schema.app.DTOCreator'
    args "--output", "$rootDir/svws-db-dto/src/main/java", "--persistence", "$rootDir/svws-db/src/main/resources/META-INF/persistence.xml"
}

assemble.dependsOn createJavaDTOs
sourcesJar.dependsOn createJavaDTOs
