import org.codehaus.groovy.runtime.GStringImpl

// Initialisierung der Plugins
plugins {
	id 'svws.gradle.node.plugin'
}

// Führt alle Tasks von subproject-plugin.gradle in diesem Verzeichnis aus
apply from: ('../subproject-plugin.gradle')

// Erzeuge eine unique JobId welche in den Tasks, temporären Dateien und Container verwendet wird.
def jobId = UUID.randomUUID().toString().substring(0, 8)

/**
 * Konfiguration für das Subpackage bzw. die Tests
 */
ext {
	SVWS_SERVERMODE = "dev"

	// Docker Konfiguration
	APP_CONTAINER_NAME = "app-vite-dav-tests${generateContainerSuffix(jobId)}"
	DB_CONTAINER_NAME = "database-vite-dav-tests${generateContainerSuffix(jobId)}"
	DB_SERVICE_NAME = "mariadb${generateContainerSuffix(jobId)}"
	APP_IMAGE_NAME = "svws-app${generateContainerSuffix(jobId)}"

	// Auch für Docker. Forwarded Ports, werden nur bei lokal genutzten Container verwendet
	FORWARDED_DB_PORT = 3319
	FORWARDED_APP_PORT = 5019

	// Liste der Datenbanken die für die Tests genutzt / geseedet werden sollen
	TARGET_TEST_DATA_BASES = ['GymAbiDav']
}

/**
 * Starten der Integrationstests gegen die Docker Umgebung
 */
tasks.register('testAgainstDockerEnv', NpmRun) {
	description = 'Startet die Integrationstests gegen die Docker Umgebung'
	group = 'verification'
	dependsOn ':svws-webclient:core:assemble'
	dependsOn 'seedDatabase'

	// Vite darf gegen Server laufen mit selbst signierten Zertifikaten
	environment NODE_TLS_REJECT_UNAUTHORIZED: 0

	// Falls Exceptions auftreten führen diese nicht zum Abbruch des Scripts
	ignoreExitValue = true

	doFirst {
		mkdir file("$projectDir/build/test-results")
	}

	// Die Ziel-url vom Container mit der Application
	if (System.getenv('CI_SERVER')) {
		environment VITE_targetHost: "https://$project.ext.APP_CONTAINER_NAME:8443"
	} else {
		environment VITE_targetHost: "https://localhost:$project.ext.FORWARDED_APP_PORT"
	}

	outputs.dir file("$projectDir/build/test-results")
	outputs.upToDateWhen { false }

	// npm Kommando
	args = ['test:tempDockerEnv']
}

/**
 * Starten der Tests gegen die lokale Applikation. Hier wird kein Docker gestartet.
 */
tasks.register('no_auto_docker_test_DAV', NpmRun) {
	group = 'verification'
	dependsOn ':tests:checkIfLokalApplikationIsRunning'
	dependsOn ':tests:checkIfLokalDataBaseIsRunning'

	inputs.files fileTree("$projectDir/tests"),
			fileTree("$rootDir").matching { exclude 'build', 'dist' }

	// npm Kommando
	args = ['test:local']
}
