def linuxfileAppDir = "$projectDir/svws/app"

dependencies {
	implementation project(':svws-server-app')
}

task clearAppFolder(type: Delete) {
	//dependsOn downloadPublishedArtifacts
	file("${linuxfileAppDir}").deleteDir()
}

/*
 Task lädt die SVWS-Artefakte einer angegebenen Version
 aus dem Package-Repository von GitHub und speichert diese
 für die Integration in der tar.gz des SVWSs.
 */

task copyArtifacts(type: Copy) {
	dependsOn clearAppFolder
	description = 'Kopieren der aktuellen Artefakte'
	// wenn doZip im build ausgeführt wird, muss dieser Task darauf warten
	mustRunAfter(':svws-webclient:client:doZip')
	mustRunAfter(':svws-webclient:admin:doZip')
	into linuxfileAppDir
	into ("lib/") {
		from (configurations.runtimeClasspath) {
			exclude "svws-*.jar"
		}
	}
	into ("/") {
		from (configurations.runtimeClasspath) {
			include "svws-*.jar"
		}
		from "../../svws-webclient/client/build/SVWS-Client.zip"
	}
	into ("/") {
		from (configurations.runtimeClasspath) {
			include "svws-*.jar"
		}
		from "../../svws-webclient/admin/build/SVWS-Admin-Client.zip"
	}
}



/*
 Verpackt die SVWS-Artefakte als tar.gz
 */
task linuxZipBuild(type: Tar, dependsOn: copyArtifacts) {
	archiveBaseName = "linux-installer"
	archiveVersion = project.ext.svwsArtifactVersion
	archiveExtension = "tar.gz"
	compression = Compression.GZIP
	from ("$projectDir/svws"){
		into "svws"
	}
	from ("$projectDir/init-scripts"){
		into "init-scripts"
	}
}


task replacePlaceholder(type: Copy, dependsOn: linuxZipBuild) {
	from "$projectDir/install.sh"

	def placeholder_name = 'LINUX_INSTALLER_FILE_NAME'
	def filename = "linux-installer-"+project.ext.svwsArtifactVersion+".tar.gz"
	filter { String line -> line.replaceAll(placeholder_name, filename) }

	def placeholder_dl = 'BASE_DOWNLOAD_URL'
	def baseUrl = "https://github.com/SVWS-NRW/SVWS-Server/releases/download/v"+project.ext.svwsArtifactVersion
	filter { String line -> line.replaceAll(placeholder_dl, baseUrl) }

	into "$buildDir/distributions"
}

task removeCR {
	dependsOn('replacePlaceholder')
	def inputFile = file("$buildDir/distributions/install.sh")
	doLast {
		ant.replace(file: inputFile) {
			replacefilter(token: '\r', value: '')
		}
	}
}

task renameInstallScript(type: Copy){
	dependsOn('removeCR')
	def inputFile = file("$buildDir/distributions/install.sh")
	from inputFile
	into "$buildDir/distributions/"
	rename { String fileName ->
		fileName.replace("install.sh", "install-"+project.ext.svwsArtifactVersion+".sh")
	}
}


task buildLinuxInstaller() {
	dependsOn('renameInstallScript')
	group "build"
}

clean.doLast {
	file("${linuxfileAppDir}").deleteDir()
}
