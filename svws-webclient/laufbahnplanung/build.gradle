plugins {
	id 'svws.gradle.node.plugin'
}

task assembleLaufbahnplanung(type: NpmRun) {
	dependsOn rootProject.npmPrepare 
	dependsOn project(':svws-webclient:core').tasks.assemble 
	dependsOn ':svws-webclient:ui:assemble'
	dependsOn ':svws-webclient:components:assemble'
	group "build"
	args = ['build']
	inputs.files fileTree("$rootDir/svws-webclient/core").matching { exclude "build", "dist" }, 
		fileTree("$rootDir/svws-webclient/ui").matching { exclude "build", "dist", ".histoire" }, 
		fileTree("$rootDir/svws-webclient/components").matching { exclude "build", "dist", ".histoire" }, 
		fileTree("$projectDir").matching { exclude "build" }
	outputs.dir file("$buildDir/output")
}

assemble.dependsOn assembleLaufbahnplanung

task doZip(type: Zip) {
	dependsOn assembleLaufbahnplanung
    archiveFileName = "SVWS-Laufbahnplanung.zip"
    destinationDirectory = file("$buildDir")
    from "$buildDir/output"
}

assemble.dependsOn doZip

task cleanLaufbahnplanung(type: Delete) {
	delete file("$buildDir/output")
}

clean.dependsOn cleanLaufbahnplanung

task publishNexusLaufbahnplanung(type: NpmPublish) {
	dependsOn assemble
	group "publishing"
	npmrc = "$rootDir/.npmrc.nexus"
	repository = "https://artifactory.svws-nrw.de/repository/npm-private/"
	actor = project.ext.getNexusActor()
	token = project.ext.getNexusToken()
	scopePublic = false
}

task publishNpmLaufbahnplanung(type: NpmPublish) {
	dependsOn assemble
	group "publishing"
	npmrc = "$rootDir/.npmrc.npm"
	repository = "https://registry.npmjs.org/"
	token = project.ext.getNpmToken()
	scopePublic = true
	tokenOnly = true
}

publishReleaseAll.dependsOn publishNexusLaufbahnplanung
publishReleaseAll.dependsOn publishNpmLaufbahnplanung

project.tasks.withType(PublishToMavenRepository).configureEach {
	dependsOn ':svws-webclient:core:publishNexusCore'
	dependsOn ':svws-webclient:ui:publishNexusUi'
//	dependsOn ':svws-webclient:components:publishNexusComponents'
	dependsOn publishNexusLaufbahnplanung
	dependsOn ':svws-webclient:core:publishNpmCore'
	dependsOn ':svws-webclient:ui:publishNpmUi'
//	dependsOn ':svws-webclient:components:publishNpmComponents'
	dependsOn publishNpmLaufbahnplanung
}

project.tasks.withType(Sign).configureEach {
	dependsOn ':svws-webclient:core:publishNexusCore'
	dependsOn ':svws-webclient:ui:publishNexusUi'
//	dependsOn ':svws-webclient:components:publishNexusComponents'
	dependsOn publishNexusLaufbahnplanung
	dependsOn ':svws-webclient:core:publishNpmCore'
	dependsOn ':svws-webclient:ui:publishNpmUi'
//	dependsOn ':svws-webclient:components:publishNpmComponents'
	dependsOn publishNpmLaufbahnplanung
}


publishing {
    repositories {
        maven {
            name = "GitHubPackagesSVWSLaufbahnplanung"
            url = "https://maven.pkg.github.com/SVWS-NRW/SVWS-Server"
            credentials {
            	username = getGithubActor()
                password = getGithubToken()
            }
        }
    }
    publications {
        mavenClientZip(MavenPublication) {
            artifact doZip
            artifactId = 'svws-laufbahnplanung'
            pom {
                name = 'SVWS-Laufbahnplanung'
                description = 'Stellt die SVWS-Laufbahnplanung als Webanwendung zur Verf√ºgung.'
                url = 'http://www.svws.nrw.de'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection = 'scm:git@github.com/SVWS-NRW/SVWS-Server'
                    developerConnection = 'scm:git@github.com/SVWS-NRW/SVWS-Server.git'
                    url = 'https://github.com/SVWS-NRW/SVWS-Server.git'
                }
                developers {
                    developer {
                        id = 'svws-nrw'
                        name = 'Schulverwaltung NRW'
                        email = 'npm@svws-nrw.de'
                    }
                }
            }
        }
    }
}
