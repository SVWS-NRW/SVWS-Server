plugins {
	id 'svws.gradle.node.plugin'
}

configurations {
	implementation.extendsFrom(jetty, resteasy, swagger, validation)
	javacore
	javaapi
}
 
dependencies {
	implementation project(':svws-core')
	implementation project(':svws-db-utils')
	implementation project(':svws-openapi')

	implementation 'org.codehaus.plexus:plexus-utils:3.4.1'

	javacore fileTree(dir: "$rootDir/svws-core/src/main/java", includes: ['**/svws_nrw/core/**/*.java', '**/svws_nrw/schuldatei/*/data/**/*.java', '**/svws_nrw/schuldatei/*/utils/**/*.java'], excludes: ['**/svws_nrw/core/transpiler/TranspilerDTO.java'])

	javaapi fileTree(dir: "$rootDir/svws-openapi/src/main/java", includes: ['**/svws_nrw/api/server/*.java', '**/svws_nrw/api/privileged/API*.java']) 
}

checkstyleMain.enabled = false
checkstyleTest.enabled = false

task removeTranspiledCode(type: Delete) {
	group "svws"
	delete fileTree(dir: "$rootDir/svws-webclient/core/src", includes: ["**/*.ts"]), fileTree(dir: "$rootDir/svws-transpile/build/tmp/transpiler", includes: ["**/*.class"])
}

task prepareTranspile {
	doLast {
		file("build/transpiler-java-files.txt").text = configurations.javacore.files.toString().replace("[", "").replace("]", "").trim()
		file("build/transpiler-api-files.txt").text = configurations.javaapi.files.toString().replace("[", "").replace("]", "").trim()
	}
	group "svws"
	dependsOn compileJava
	inputs.files configurations.javacore.files, configurations.javaapi.files
	outputs.files "build/transpiler-java-files.txt", "build/transpiler-api-files.txt"
}

task transpileCore(type: JavaExec) {
	group = "svws"
	dependsOn prepareTranspile
	dependsOn project(":svws-core").tasks.assemble
	dependsOn project(":svws-openapi").tasks.assemble
	doFirst {
		delete fileTree(dir: "$buildDir/ts"), fileTree(dir: "$buildDir/tmp/transpiler/ts", includes: ["**/*.class"])
	}
	inputs.files "build/transpiler-java-files.txt", sourceSets.main.resources.getSourceDirectories()
	outputs.dir "$buildDir/ts"
	description = "Transpiliert den Java-Code nach Type-/Javascript"
	classpath = sourceSets.main.runtimeClasspath
	mainClass = "de.svws_nrw.transpiler.app.TranspileTs"
	args "--output", "$buildDir/ts",
		"--ignore", "de.svws_nrw",
		"--tmpdir", "$buildDir/tmp/transpiler/ts",
		"--files", "build/transpiler-java-files.txt"
	jvmArgs "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"
}

task transpileApi(type: JavaExec) {
	group = "svws"
	dependsOn prepareTranspile
	dependsOn project(":svws-core").tasks.assemble
	dependsOn project(":svws-openapi").tasks.assemble
	doFirst {
		delete fileTree(dir: "$rootDir/svws-webclient/core/src"), fileTree(dir: "$buildDir/tmp/transpiler/tsapi", includes: ["**/*.class"])
	}
	inputs.files "build/transpiler-java-files.txt", "build/transpiler-api-files.txt", sourceSets.main.resources.getSourceDirectories()
	outputs.dir "$buildDir/tsapi"
	description = "Transpiliert den Java-Code nach Type-/Javascript"
	classpath = sourceSets.main.runtimeClasspath
	mainClass = "de.svws_nrw.transpiler.app.GenerateTsOpenApi"
	args "--output", "$buildDir/tsapi",
		"--ignore", "de.svws_nrw",
		"--tmpdir", "$buildDir/tmp/transpiler/tsapi",
		"--files", "build/transpiler-api-files.txt"
	jvmArgs "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"
}

task transpileAll(type: Copy) {
	group = "svws"
	dependsOn transpileCore
	dependsOn transpileApi
	inputs.dir "$buildDir/ts"
	inputs.dir "$buildDir/tsapi"
	outputs.dir "$rootDir/svws-webclient/core/src"
	doFirst {
		delete fileTree(dir: "$rootDir/svws-webclient/core/src", includes: ["**/*.ts"])
	}
	from "$buildDir/ts"
	from "$buildDir/tsapi"
	into "$rootDir/svws-webclient/core/src"
	include "**/*.ts"
	exclude "index.ts"
	doLast {
		(new File("$rootDir/svws-webclient/core/src/index.ts")).text = file("$buildDir/ts/index.ts").getText() + file("$buildDir/tsapi/index.ts").getText()
	}
}

assemble.dependsOn transpileAll


task testTranspilerCore(type: NpmRun) {
	group "svws"
	dependsOn rootProject.npmPrepare, test
	mustRunAfter test
	workingDir file("$projectDir/src/test/ts")
	inputs.files fileTree(dir: "$projectDir/src/main/resources/typescript", includes: ["**/*.ts"]),
		fileTree(dir: "$projectDir/build/tests/ts", includes: ["**/*.ts"]),
		fileTree(dir: "$projectDir/src/test/ts/src", includes: ["**/*.test.ts", "**/*.bench.ts"]),
		file("$projectDir/src/test/ts/package.json"), file("$projectDir/src/test/ts/tsconfig.json"), file("$projectDir/src/test/ts/vitest.config.ts")
	outputs.dir file("$projectDir/build/coverage")
	args = [ 'test:run' ]
	doFirst {
		mkdir file("$projectDir/build/coverage")
	}
}

check.dependsOn testTranspilerCore


