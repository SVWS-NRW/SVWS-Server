plugins {
	id 'svws.gradle.node.plugin'
}

configurations {
	implementation.extendsFrom(jetty, resteasy, swagger, validation)
}
 
dependencies {
	implementation 'org.codehaus.plexus:plexus-utils:4.0.1'
}

checkstyleMain.enabled = false
checkstyleTest.enabled = false

test {
	jvmArgs "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"
}

task testTranspilerCore(type: NpmRun) {
	group "verification"
	dependsOn rootProject.npmPrepare, test
	mustRunAfter test
	workingDir file("$projectDir/src/test/ts")
	inputs.files fileTree(dir: "$projectDir/src/main/resources/typescript", includes: ["**/*.ts"]),
		fileTree(dir: "$projectDir/build/tests/ts", includes: ["**/*.ts"]),
		fileTree(dir: "$projectDir/src/test/ts/src", includes: ["**/*.test.ts", "**/*.bench.ts"]),
		file("$projectDir/src/test/ts/package.json"), file("$projectDir/src/test/ts/tsconfig.json"), file("$projectDir/src/test/ts/vitest.config.ts")
	outputs.dir file("$projectDir/build/coverage")
	args = [ 'test:run' ]
	doFirst {
		mkdir file("$projectDir/build/coverage")
	}
}

check.dependsOn testTranspilerCore

apply from: "$rootDir/publish-helper.gradle"

publishing {
  publications {
    publishJava(MavenPublication) {
      artifactId = 'svws-transpile'
      pom {
        name = 'SVWS Bibliothek für das Transpilieren von Java-Code nach Typecript.'
        description = 'Diese Bibliothek ermöglicht das Transpilieren von Java-Code nach Typecript.'
      }
    }
  }
}

