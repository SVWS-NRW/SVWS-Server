configurations {
    implementation.extendsFrom(crypto, database, jackson, validation, jetty, resteasy)
}

// add generated java files to sourceSets
sourceSets {
    main {
        java {
            srcDirs += 'src/main/gen'
        }
    }
}

//exclude javadoc for generated model without comments
javadoc {
    exclude 'de/svws_nrw/davapi/model/**'
}

//add new source folder to eclipse
eclipse {
    classpath {
        file.whenMerged { cp ->
            def src = new org.gradle.plugins.ide.eclipse.model.SourceFolder('src/main/gen', null)
            //deactivate optional warnings in eclipse for this source folder
            src.entryAttributes.put("ignore_optional_problems", "true")
            cp.entries.add( src )
        }
    }
}

compileJava.dependsOn(":svws-db:assemble")
compileJava.dependsOn(":svws-db-utils:assemble")

dependencies {
    implementation project(':svws-core')
    implementation project(':svws-db')
    implementation project(':svws-db-dto')
    implementation project(':svws-db-utils')
}

publishing {
    publications {
        mavenSVWSDavAPI(MavenPublication) {
            artifactId = 'svws-module-dav-api'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'SVWS DAV-API Library'
                description = 'Diese Bibliothek enthält die Java-Server-Definition der CalDAV und CardDAV-Schnittstelle für die Schulverwaltungssoftware in NRW'
                url = 'http://www.svws.nrw.de'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection = 'scm:git@github.com/SVWS-NRW/SVWS-Packages'
                    developerConnection = 'scm:git@github.com/SVWS-NRW/SVWS-Packages.git'
                    url = 'https://github.com/SVWS-NRW/SVWS-Packages.git'
                }
            }
        }
    }
}



